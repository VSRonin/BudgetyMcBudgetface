cmake_minimum_required(VERSION 3.14)
project(BudgetyMcBudgetface LANGUAGES CXX)
set(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake/modules ${CMAKE_MODULE_PATH})
include(GetGitRevisionDescription)
git_describe(GitTagVersion --tags)
string(REGEX MATCH "^[0-9]+\\.[0-9]+\\.[0-9]+.*" VERSION_MATCH "${GitTagVersion}")
if(VERSION_MATCH)
    string(REGEX REPLACE "^([0-9]+)\\..*" "\\1" VERSION_MAJOR "${GitTagVersion}")
    string(REGEX REPLACE "^[0-9]+\\.([0-9]+).*" "\\1" VERSION_MINOR "${GitTagVersion}")
    string(REGEX REPLACE "^[0-9]+\\.[0-9]+\\.([0-9]+).*" "\\1" VERSION_PATCH "${GitTagVersion}")
else()
    set(VERSION_MAJOR 0)
    set(VERSION_MINOR 0)
    set(VERSION_PATCH 0)
endif()
set(VERSION_SHORT "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}")
message("Budgety McBudgetface Version ${VERSION_SHORT}")
if("${CMAKE_SIZEOF_VOID_P}" STREQUAL "4")
    set(BudgetFace_IS64BITS OFF)
else()
    set(BudgetFace_IS64BITS ON)
endif()
if(BudgetFace_IS64BITS)
    set(BudgetFace_PlatformDir "x64")
else()
    set(BudgetFace_PlatformDir "x86")
endif()
add_subdirectory(src)
install(FILES "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE" DESTINATION "BudgetyMcBudgetface/licenses")
SET(CPACK_PACKAGE_HOMEPAGE_URL "https://github.com/VSRonin/BudgetyMcBudgetface")
SET(CPACK_PACKAGE_VERSION_MAJOR ${VERSION_MAJOR})
SET(CPACK_PACKAGE_VERSION_MINOR ${VERSION_MINOR})
SET(CPACK_PACKAGE_VERSION_PATCH ${VERSION_PATCH})
SET(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_CURRENT_SOURCE_DIR}/README.md")
SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE")
SET(CPACK_PACKAGE_FILE_NAME "BudgetyMcBudgetface-${VERSION_SHORT}-${CMAKE_SYSTEM_NAME}-${BudgetFace_PlatformDir}")
include(CPack)
